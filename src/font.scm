;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; filename: font.scm
;;
;; description: Runtime libraries for fonts. Contains functions
;; related either to the font loading/generation or usage. Some
;; function are thus generated by the macros in font-macro.scm and
;; should not be used by a user. The "exported" functions are marked
;; in the last section of this file
;;
;; author: David St-Hilaire
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(include "../include/scm-lib_.scm")


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Font image parsing
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; used to debug print characters gotten from get-font-table into
;; stdout
(define (test char w h)
  (for y 0 (< y h)
    (for x 0 (< x w)
      (let ((p (list-ref char (+ (* y w) x))))
        (if (> (apply + p) 0)
            (display "x")
            (display "_"))
        (if (= x (- w 1)) (newline))))))

(define (get-char pixels-vector x y width height char-width char-height)
  (let* ((x0 (- width (* (+ x 1) char-width)))
         (y0 (- height (* (+ y 1) char-height)))
         (x-max (+ x0 char-width))
         (y-max (+ y0 char-height)))
    (let loopy ((y y0)(acc-y '()))
      (if (< y y-max)
          (loopy
           (+ y 1)
           (append 
            (let loopx ((x x0) (acc-x '()))
              (if (< x x-max)
                  (loopx (+ x 1)
                         (cons
                          (vector-ref pixels-vector (+ (* y width) x))
                          acc-x))
                  (reverse acc-x)))
            acc-y))
          (reverse acc-y)))))

(define (get-font-table font-name char-width char-height)
  (let* ((image-file     (string-append "fonts/" font-name ".ppm"))
         (font-data-file (string-append "fonts/" font-name ".scm"))
         (ppm-data (parse-ppm-image-file image-file))
         (font-data (with-input-from-file font-data-file
                      (lambda () (read))))
         (width (ppm-image-width ppm-data))
         (height (ppm-image-height ppm-data))
         (pixels (list->vector (ppm-image-pixels ppm-data)))
         (x-max (quotient width char-width))
         (y-max (quotient height char-height))
         (char-table (make-table)))
    (if (not (and (= (modulo width char-width) 0)
                  (= (modulo height char-height) 0)))
        (error "invalid font image char size."))
    (let ((chars-pixels
           (let loopy ((y 0)(acc-y '()))
             (if (< y y-max)
                 (loopy
                  (+ y 1)
                  (append
                   (let loopx ((x 0) (acc-x '()))
                     (if (< x x-max)
                         (loopx (+ x 1)
                                (cons (get-char pixels x y width height
                                                char-width char-height)
                                      acc-x))
                         acc-x))
                   acc-y))
                 (reverse acc-y)))))
      (let loop-colors ((colors (cadr (assq colors: font-data)))
                 (chars-pixels chars-pixels))
        (if (not (pair? colors))
            char-table
            (let loop-chars ((chars (cadr (assq chars: font-data)))
                             (chars-pixels chars-pixels))
              (if (not (pair? chars))
                  (loop-colors (cdr colors) chars-pixels)
                  (begin
                    (table-set! char-table (list (car colors) (car chars))
                                (car chars-pixels))
;;                     (pp `(set (,(car colors) ,(car chars)) to:))
;;                     (test (car chars-pixels) 8 8)
                    (loop-chars (cdr chars) (cdr chars-pixels))))))))))



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Internal runtime functions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Fonction used to dynamically load the font image data into the
;; pre-declared C array.
(define (load-font-char font-name char-table-data update-fun
                        char-width char-height
                        good-char-width good-char-height)
  (let* ((color (caar char-table-data))
         (char (cadar char-table-data))
         (pixels (cdr char-table-data))
         (char-index (get-char-index font-name color char)))
    (for y 0 (< y good-char-height)
      (for x 0 (< x good-char-width)
        (let* ((oob?
                (or (>= x char-width) (>= y char-height)))
               (current-pix
                (if (not oob?)
                    (list-ref pixels (+ (* y char-width) x))
                    '()))
               (r (if oob? 0 (car current-pix)))
               (g (if oob? 0 (cadr current-pix)))
               (b (if oob? 0 (caddr current-pix)))
               (a (if oob? 0 (if (< (+ r g b) 10) 0 255))))
          (update-fun char-index y x r g b a))))))



;; Font data type definition. The get-pointer parameter should be a
;; function taking a integer (let's call it 'i') argument, and it will
;; return the pointer corresponding to the 'i'th char image in the font.
(define-type font id texture-table width height get-pointer)

;; Global variable that will contain all the registered (via
;; (define-symmetric-font ...)) font data.
(define global-fonts-table (make-table))

;; This function is used to pre-generate indices for each (color,char)
;; combinations such that if called at macro-expension time or
;; runtime, the indices will be consistent.
(define (init-char-indexes! font-name)
  (define font-data
    (with-input-from-file (string-append "fonts/" font-name ".scm")
      (lambda () (read))))
  ;;(pp `(initilizing indexes for ,font-name))
  (let* ((colors (cadr (assq colors: font-data)))
         (chars (cadr (assq chars: font-data))))
    (for-each
     (lambda (color)
       (for-each (lambda (char)
                   (get-char-index font-name color char))
                 chars))
     colors)))

;; Returns the index of the (color,char) of the given font object
(define get-char-index
  ;; will create a new index generator to be used with a font.
  (let ((create-index-generator (lambda ()
                                  (let ((id -1))
                                    (lambda () (set! id (+ id 1)) id))))
        (index-table (make-table)))
    ;;main function
    (lambda (font-name color char)
      ;; lookup in the table if the key was already set
      (let* ((key (list font-name color char))
             (index (table-ref index-table key #f)))
        (if (not index)
            ;; if not, then get the generator for the corresponding
            ;; font, or create one if none is present and generate a
            ;; new index for that key
            (let* ((index-gen (let ((gen (table-ref index-table font-name #f)))
                          (if (not gen)
                              (let ((i-gen (create-index-generator)))
                                (table-set! index-table font-name i-gen)
                                (set! gen i-gen)))
                          gen))
                   (new-index (index-gen)))
              (table-set! index-table key new-index)
              (set! index new-index)))
        index))))

;; Register a new font data into the global table
(define (add-new-font! font-name font-obj)
  (table-set! global-fonts-table font-name font-obj))

(define texture-cache-id '())
(define texture-cache-tex '())
(define (load-texture-cache x)
  (if (equal? x texture-cache-id)
      texture-cache-tex
      #f))
(define (texture-cache-set! id tex)
  (set! texture-cache-id tex)
  (set! texture-cache-texture tex)
  tex)

(define (find-font-texture font color char)
  (let ((id (list font color char)))
   (cond
    ((load-texture-cache id) => (lambda (x) x))
    (else (texture-cache-set!
           id
           (table-ref (font-texture-table font) (list color char)))))))

(define (upload-font-to-video-card font #!key
                                   (wrap-s GL_CLAMP)
                                   (wrap-t GL_CLAMP)
                                   (mag GL_NEAREST)
                                   (min GL_NEAREST))
  (lambda (color char tex-id)
    (glBindTexture GL_TEXTURE_2D tex-id)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_S wrap-s)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_T wrap-t)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER mag)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER min)
    (let* ((ptr-index (get-char-index (font-id font) color char))
           (ptr ((font-get-pointer font) ptr-index)))
      (glTexImage2D GL_TEXTURE_2D 0 GL_RGBA
                    (font-width  font)
                    (font-height font)
                    0 GL_RGBA GL_UNSIGNED_BYTE ptr))
    ))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; External runtime functions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Draw the character of given color and font at coordinate (x',y)
;; where x' = x + char-width * x-offset.
(define (draw-char font color char x y x-offset)
  (let ((texture-obj (find-font-texture font color char)))
    (draw-texture texture-obj
                  (+ x (* x-offset (texture-width texture-obj)))
                  y)))

(define (draw-textured-object font color char x y width height)
  (let ((texture-obj (find-font-texture font color char)))
    (draw-texture texture-obj x y width: width height: height)))